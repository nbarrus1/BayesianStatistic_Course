model 
  {
		for (i in 1:N){ 
			# model prediction  
			PredL[i] <- L1 +(L2-L1)* (1 - exp(- K* (Age[i] -Age1)))/(1 - exp(- K* (Age2 -Age1))) 	
			ObsL[i] ~ dnorm(PredL[i], tau)  	# lognormal likelihood
			 resid[i] <- ObsL[i]-PredL[i]  # residuals
		 	Rep[i] ~ dnorm(PredL[i], tau) # replicated data set
		  sresid2[i]<-(ObsL[i]-PredL[i])*(ObsL[i]-PredL[i])*tau   
      rep.sresid2[i]<-(Rep[i]-PredL[i])*(Rep[i]-PredL[i])*tau 
      LL[i] <- -0.5*log(2*3.14159)+0.5*log(tau)-0.5*tau*(ObsL[i]-PredL[i])*(ObsL[i]-PredL[i])
		}
  #priors specification
     K ~ dunif(0,2)
	   Age1<-1
	   Age2<-10
     L1~dunif(10,800) 	
     L2~dunif(10,800) 	
     tau~dgamma(0.001,0.001)
  #Derived parameters
    Linf<- (L2-L1*exp(-K*(Age2-Age1)))/(1-exp(-K*(Age2-Age1)))
    chi.square.obs<-sum(sresid2[])
    chi.square.rep<-sum(rep.sresid2[])
    p.value<-step(chi.square.obs-chi.square.rep)
    dev <- -2*sum(LL[])
}

