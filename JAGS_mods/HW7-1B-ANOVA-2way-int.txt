model   
{ #two-way ANOVA with interaction term, using reference classes

  #set up contrast priors- noninteractions
  base ~ dnorm(0, 1.0E-6)
  a[1] <- 0   #reference location
  a[2] ~ dnorm(0.0, 1.0E-6)
  a[3] ~ dnorm(0.0, 1.0E-6)
  a[4] ~ dnorm(0.0, 1.0E-6) 
  b[1] <- 0   #reference bait
  b[2] ~ dnorm(0.0, 1.0E-6)
  b[3] ~ dnorm(0.0, 1.0E-6)
  
  #set up contrast priors- interactions
  int[1,1] <- 0
  int[2,1] <- 0
  int[3,1] <- 0
  int[4,1] <- 0
  int[1,2] <- 0
  int[2,2] ~ dnorm(0.0, 1.0E-6)
  int[3,2] ~ dnorm(0.0, 1.0E-6)
  int[4,2] ~ dnorm(0.0, 1.0E-6)
  int[1,3] <- 0
  int[2,3] ~ dnorm(0.0, 1.0E-6)
  int[3,3] ~ dnorm(0.0, 1.0E-6)
  int[4,3] ~ dnorm(0.0, 1.0E-6)

  tau ~ dgamma(0.001, 0.001)  # uninformative precision

#model

  for (i in 1:N) # for each of the samples
  {
    ymean[i] <- base + a[X1[i]] + b[X2[i]] + int[X1[i],X2[i]]
    Y[i] ~ dnorm(ymean[i], tau)
    LL[i] <- -0.5*log(2*3.14159)+0.5*log(tau)-0.5*tau*(Y[i]-ymean[i])*(Y[i]-ymean[i])
  }
}

